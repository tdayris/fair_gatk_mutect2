include: "rules/common.smk"


if lookup_config(dpath="load_fair_genome_indexer_pipeline", default=True):

    include: "rules/fair_genome_indexer_pipeline.smk"


align_with_star: bool = lookup_config(dpath="load_star_mapping", default=False)
align_with_bowtie2: bool = lookup_config(dpath="load_bowtie2_mapping", default=True)
if align_with_bowtie2 and not align_with_star:

    include: "rules/fair_bowtie2_mapping_pipeline.smk"

elif align_with_star and not align_with_bowtie2:

    include: "rules/fair_star_mapping_pipeline.smk"


if lookup_config(dpath="load_fair_fastqc_multiqc_pipeline", default=True):

    include: "rules/fair_fastqc_multiqc_pipeline.smk"


include: "rules/bcftools.smk"
include: "rules/gatk_mutect2_calling_meta.smk"
include: "rules/gatk.smk"
include: "rules/multiqc.smk"
include: "rules/snpeff.smk"
include: "rules/snpsift.smk"


rule gatk_mutect2_targets:
    input:
        unpack(get_gatk_mutect2_targets),
    output:
        touch("tmp/targets/gatk_mutect2_targets.txt"),
    log:
        "logs/gatk_mutect2_targets.log",
    benchmark:
        "benchmark/gatk_mutect2_targets.tsv"
